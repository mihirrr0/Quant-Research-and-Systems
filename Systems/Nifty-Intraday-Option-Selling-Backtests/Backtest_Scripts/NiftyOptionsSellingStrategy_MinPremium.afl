_SECTION_BEGIN("Price");

// Chart setup and configuration
SetChartOptions(0, chartShowArrows | chartShowDates);
_N(Title = StrFormat("{{NAME}} - {{INTERVAL}} {{DATE}} Open %g, High %g, Low %g, Close %g (%.1f%%) {{VALUES}}", O, H, L, C, SelectedValue(ROC(C, 1))));
Plot(C, "Close", ParamColor("Chart Color", colorDefault), styleNoTitle | ParamStyle("Style") | GetPriceStyle());

// Strategy settings
#include <Nifty_Strike_Selector.afl>  // Include strike price selection logic.

SetOption("InitialEquity", 10000000);
SetOption("AccountMargin", 100);
SetOption("MaxOpenPositions", 2);
SetTradeDelays(0, 0, 0, 0);
SetOption("DisableRuinStop", True);

// Entry timing optimization cases
Cases = Optimize("Entry Cases", 0, 0, 14, 2);

if (Cases == 0) { EntryTime = 091500; }
if (Cases == 1) { EntryTime = 093000; }
if (Cases == 2) { EntryTime = 094500; }
if (Cases == 3) { EntryTime = 100000; }
if (Cases == 4) { EntryTime = 101500; }
if (Cases == 5) { EntryTime = 103000; }
if (Cases == 6) { EntryTime = 104500; }
if (Cases == 7) { EntryTime = 110000; }
if (Cases == 8) { EntryTime = 111500; }
if (Cases == 9) { EntryTime = 113000; }
if (Cases == 10) { EntryTime = 114500; }
if (Cases == 11) { EntryTime = 120000; }
if (Cases == 12) { EntryTime = 121500; }
if (Cases == 13) { EntryTime = 123000; }
if (Cases == 14) { EntryTime = 124500; }
if (Cases == 15) { EntryTime = 130000; }

ExitTime = 152000;

// Bar index and conditions for exit
bi = BarIndex();
exitLastBar = bi == LastValue(bi - 1);

// Exclude trades on Diwali dates
NonDiwaliDays = DateNum() != 1111026 AND DateNum() != 1121113 AND DateNum() != 1131103 AND DateNum() != 1141023 AND DateNum() != 1151111 AND DateNum() != 1161030 AND DateNum() != 1171019 AND DateNum() != 1181107 AND DateNum() != 1191027 AND DateNum() != 1201114;

// Signal generation for short positions
Short = StrikePriceSelector == 1 AND TimeNum() == EntryTime AND NonDiwaliDays;
ShortPrice = Close;

// Signal generation for exiting short positions
Cover = TimeNum() == ExitTime OR exitLastBar OR DateNum() != Ref(DateNum(), 1);
CoverPrice = Close;

Short = ExRem(Short, Cover);
Cover = ExRem(Cover, Short);

// Position sizing parameters
EquityPercent = Optimize("POE", 2, 1, 5, 1);

Length = IIf(StrLen(Name()) == 18, 16, 15);
CallSymbol = StrLeft(Name(), Length) + "CE";
PutSymbol = StrLeft(Name(), Length) + "PE";
CallPremium = Foreign(CallSymbol, "Close");
PutPremium = Foreign(PutSymbol, "Close");

LotSize = IIf(DateNum() >= 1110101 AND DateNum() <= 1141031, 50,
          IIf(DateNum() >= 1141031 AND DateNum() <= 1151030, 25,
          IIf(DateNum() >= 1151031 AND DateNum() <= 1210531, 75, 50)));

NSELeverage = 12;
ExposureMargin = Foreign("$NIFTY-NSE", "C") * 2 * 0.02 * LotSize;
SpanMargin = Moneyness * LotSize;
TotalMargin = ExposureMargin + SpanMargin;
MISLeverage = 12;
Deposit = (Foreign("$NIFTY-NSE", "C") * LotSize * 2) / MISLeverage;
MinPremium = (Deposit * (EquityPercent / 100)) / LotSize;
ActualMinPremium = (((Foreign("$NIFTY-NSE", "C") * LotSize * 2) / NSELeverage) * (EquityPercent / 100)) / LotSize;

// Dynamic selection logic for options
DynamicCallSelection = abs(CallPremium - MinPremium);
DynamicPutSelection = abs(PutPremium - MinPremium);
CallFactor = IIf(CallPremium >= ActualMinPremium, 1, CallPremium / ActualMinPremium);
PutFactor = IIf(PutPremium >= ActualMinPremium, 1, PutPremium / ActualMinPremium);

// Position scoring and sizing
PositionScore = IIf(Type == "CE" AND StrikePriceSelector == 1, 100000 - DynamicCallSelection,
              IIf(Type == "PE" AND StrikePriceSelector == 1, 100000 - DynamicPutSelection, Null));
PositionSize = IIf(Type == "CE", -CallFactor * EquityPercent, -PutFactor * EquityPercent);

// Apply stop-loss logic
StopLoss = IIf(DaysToExpiry == 0, 30,
          IIf(DaysToExpiry == 1, 40,
          IIf(DaysToExpiry == 2, 40,
          IIf(DaysToExpiry == 3, 50,
          IIf(DaysToExpiry == 6, 40, 50)))));
ApplyStop(stopTypeLoss, stopModePercent, StopLoss, 1);

// Plot entry and exit signals on the chart
PlotShapes(Short * shapeDownArrow, colorWhite, 0, H, -10);
PlotShapes(Cover * shapeUpArrow, colorWhite, 0, L, -10);

// Custom backtest processor
SetCustomBacktestProc("");

Limit = 1;
if (Status("action") == actionPortfolio) {
    bo = GetBacktesterObject();
    bo.PreProcess();

    NiftyClose = Foreign("$NIFTY-NSE", "C");

    for (i = 0; i < BarCount; i++) {
        CallCount = 0;
        PutCount = 0;

        for (sig = bo.GetFirstSignal(i); sig; sig = bo.GetNextSignal(i)) {
            Type = StrRight(sig.Symbol, 2);
            if (sig.IsEntry() AND Type == "PE") {
                if (PutCount < Limit)
                    PutCount++;
                else
                    sig.Price = -1;
            }

            if (sig.IsEntry() AND Type == "CE") {
                if (CallCount < Limit)
                    CallCount++;
                else
                    sig.Price = -1;
            }
        }

        bo.ProcessTradeSignals(i);
        bo.UpdateStats(i, 1);
        bo.UpdateStats(i, 2);
    }

    bo.PostProcess();
}

// Portfolio equity tracking
PortEquity = Foreign("~~~EQUITY", "C");
Filter = 1;
AddColumn(PortEquity, "Portfolio Equity", 1);

_SECTION_END();
